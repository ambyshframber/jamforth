32 8 * ALLOT CONSTANT LOCAL_NAMES
0 VALUE LOCALS

: ; IMMEDIATE
	LOCALS ?DUP IF
		CELLS [COMPILE] LITERAL [[ RSP+ ]]
	THEN
	[COMPILE] ;
	0 TO LOCALS
;

: LOCAL IMMEDIATE
	WORD TUCK ( l a l )
	LOCAL_NAMES LOCALS 5 << +
	DUP >R SWAP CMEMCPY
	R> + 0 SWAP C!
	1 +TO LOCALS
;

: ENDLOCALS IMMEDIATE
	LOCALS CELLS [COMPILE] LITERAL [[ RSP- ]]
;

: LOCNAME= ( a u loc# -- 0|1 )
	5 << LOCAL_NAMES +
	DUP STRLEN S=
;

: FINDLOCAL ( a u -- ofs | -1 )
	LOCALS UNLESS
		-1 EXIT
	THEN
	0 ( a u idx )
	BEGIN DUP LOCALS < WHILE
		DUP >R >R 2DUP R> LOCNAME=
		IF
			2DROP R> CELLS EXIT
		THEN
		R> 1+
	REPEAT
	DROP -1
;

: (L@) R> @4+ RSP@ + @ SWAP >R ;

: L@ IMMEDIATE
	WORD FINDLOCAL DUP -1 = IF ." LOCAL ERROR: " TEXTERR THEN
	[[ (L@) ]] ,
;

: (L!) R> @4+ RSP@ + ( v r_addr l_addr ) SWAP >R ! ;

: L! IMMEDIATE
	WORD FINDLOCAL DUP -1 = IF ." LOCAL ERROR: " TEXTERR THEN
	[[ (L!) ]] ,
;
